class GameRunner {
	field int direction;
	field Basket basket;
	field Array allBombs;
	field Array allFruits;
	field Array locations;
	field int numOfBombs;
	field int numOfFruits;
	field int life;
	field int score;
	field int currLocation;
	field int numOfLocations;
	field int addLifeCounter;
	field boolean gameOver;
	field boolean exit;
	
	/** Constructs a new Square Game. */
	constructor GameRunner new() {
		let basket = Basket.new(220, 230);
		let direction = 0;
		let score = 0;
		let addLifeCounter =0;
		let numOfBombs = 3;
		let numOfFruits = 5;
		let life = 3;
		let currLocation = 0;
		let gameOver = false;
		let exit = false;
		let numOfLocations = 17;
		return this;
    }
	
	method void updateGame() {
		var int new_speed;
		var int i;
		var int type;
		var Bomb b;
		var Fruit f;
		var boolean isCollision;
		var int nextLocation;
		let i = 0;
				
		// go throw all the bombs
		while (i<numOfBombs) {
			let b = allBombs[i];
			do b.startFalling();
			let isCollision = checkCollisionBomb(b);
			if (isCollision) {
				let addLifeCounter = 0;
				do b.erase();
				do basket.blink();
				let nextLocation = locations[currLocation];
				let currLocation = currLocation + 1;
				if (currLocation = numOfLocations){
					let currLocation = 0;
				}
				let allBombs[i] = Bomb.new(nextLocation);
				do b.dispose();
				do printScore();
				do printLife();
				if (gameOver){
					do printGameOver();
					return;
				}
			}
			let i = i + 1;
		}

		let i = 0;
		// go throw all the fruits
		while (i<numOfFruits) {
			let f = allFruits[i];
			do f.startFalling();
			let isCollision = checkCollisionFruit(f);
			if (isCollision) {
				do f.erase();
				do basket.blink();
				let type = f.getType();
				let nextLocation = locations[currLocation];
				let currLocation = currLocation + 1;
				if (currLocation = numOfLocations){
					let currLocation = 0;
				}
				if (type = 1) {
					let allFruits[i] = Fruit.new(nextLocation, 2);
				}
				if (type = 2) {
					let allFruits[i] = Fruit.new(nextLocation, 3);					
				}
				if (type = 3) {
					let allFruits[i] = Fruit.new(nextLocation, 1);
				}
				do f.dispose();
				do printScore();
				do printLife();				
			}
			let i = i + 1;
		}		
		if (direction = 1) {	// left key pressed
			do basket.moveLeft();
		}
		if (direction = 2) {	// right key pressed
			do basket.moveRight();
		}
		
	do basket.draw();
	if (score=50){
		do printWonGame();
	}
	return;
	}
   	
	method void runFullGame() {
		var char key;
		do printStartGame();
		do generateLocations();
		do generateBombs();
		do generateFruits();
		do gameBoard();
		while (~exit) {
			// waits for a key to be pressed
			while (key = 0) {
				let key = Keyboard.keyPressed();
				do updateGame();
				if (exit) {
					do Sys.wait(500);
					return;
				}
			}
			if (key = 81)  { let exit = true; }     // q key
			if (key = 130) { let direction = 1; }   // left arrow
			if (key = 132) { let direction = 2; }   // right arrow

			// waits for the key to be released
			while (~(key = 0)) {
				let key = Keyboard.keyPressed();
				do updateGame();
			}
			let direction = 0;
		} // end of while loop
		return;
	}
		
	method void generateBombs() {
		var int i;
		let allBombs = Array.new(numOfBombs);
		let i = 0;
		while(i<numOfBombs) {
			let allBombs[i] = Bomb.new(locations[currLocation]);
			let currLocation = currLocation + 1;
			if (currLocation = numOfLocations){
				let currLocation = 0;
			}
			let i = i + 1;
		}
		return;
	}

	method void generateFruits() {
		var int j;
		let allFruits = Array.new(numOfFruits);
		let j = 0;
		while(j<numOfFruits) {
			let allFruits[j] = Fruit.new(locations[currLocation],1);
			let currLocation = currLocation + 1;
			if (currLocation = numOfLocations){
				let currLocation = 0;
			}
			let j = j + 1;
		}
		return;
	}
	
	method void generateLocations() {
		let locations = Array.new(numOfLocations);
		let locations[0] = 28;
		let locations[1] = 15;
		let locations[2] = 5;
		let locations[3] = 24;
		let locations[4] = 12;
		let locations[5] = 18;
		let locations[6] = 26;
		let locations[7] = 10;
		let locations[8] = 13;
		let locations[9] = 2;
		let locations[10] = 7;
		let locations[11] = 4;
		let locations[12] = 21;
		let locations[13] = 6;
		let locations[14] = 22;
		let locations[15] = 31;
		let locations[16] = 1;
		return;	
	}

	
	method boolean checkCollisionFruit(Fruit f){
		var int xBasket;
		var int yBasket;
		var int fx;
		var int fy;
		let xBasket = basket.getX();
		let yBasket = basket.getY();
		let fx = f.getX();
		let fy = f.getY();

		if (((yBasket-17) < fy) & (fy < yBasket)& (xBasket-15< fx) & (fx < (xBasket+50))) {
			let score = score + 1;
			if (addLifeCounter = 10){
				let life = life + 1;
				do printLife();
				let addLifeCounter = 0;
			}
			else {
				let addLifeCounter = addLifeCounter + 1;
			}
			return true;
		}
	return false;
	}

	
	method boolean checkCollisionBomb(Bomb b){
		var int xBasket;
		var int yBasket;
		var int bx;
		var int by;
		let xBasket = basket.getX();
		let yBasket = basket.getY();
		let bx = b.getX();
		let by = b.getY();

		if (((yBasket-17) < by) & (by < yBasket)& (xBasket-15< bx) & (bx < (xBasket+50))) {
			let score = score - 2;
			if (score < 0){
				let score = 0;
			}
			let life = life -1;
			if (life = 0){
				let gameOver = true;
			}
			return true;
		}
	return false;
	}
	
	
	method void printScore() {
		do Output.moveCursor(0,48);
		do Output.printInt(score);
		return;
	}
	
	method void gameBoard () {
		do basket.draw();
		do Output.moveCursor(0,1);
		do Output.printString(" to exit press q  HUNGRY BASKET    Game Score:     Life: ");
		do printScore();
		do printLife();
		
		return;
	}
	
	method void printLife() {
		do Output.moveCursor(0,59);
		do Output.printInt(life);
		return;
	}
	
	method void printGameOver(){
		do Screen.clearScreen();
		do Output.moveCursor(10,22);
		do Output.printString("GAME OVER! GOODBYE");
		let exit = true;
		return;
	}
	
	method void printWonGame(){
		do Screen.clearScreen();
		do Output.moveCursor(10,10);
		do Output.printString(" OMG YOU WON THE GAME! GOOD JOB:) GOODBYE");
		let exit = true;
		return;
	}
	
	method void printStartGame(){
		do Output.moveCursor(10,15);
		do Output.printString("WELCOME TO HUNGRY BASKET GAME :)");
		do Output.moveCursor(12,8);
		do Output.printString("you need to chatch the fruits and avoid the bombs");
		do Output.moveCursor(14,26);
		do Output.printString("GOOD LUCK!");
		do Sys.wait(4000);
		do Screen.clearScreen();
		return;
	}
}
